buildscript {
    ext.kotlin_version = '1.3.60-eap-25'
    ext.ktor_version = '1.3.0-beta-1'
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'idea'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}
apply plugin: 'kotlin'

mainClassName = 'kr.ac.kw.coms.landmarks.server.ApplicationKt'

sourceSets {
    main.resources.srcDirs = [ 'src/main/res' ]
}

jar {
    baseName = 'landmarks-serverkt'
    version = '0.1'
    manifest {
        attributes 'Main-Class': 'kr.ac.kw.coms.landmarks.server.ApplicationKt'
    }
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/ktor/' }
    maven { url 'https://dl.bintray.com/kotlin/exposed/' }
    maven { url 'https://dl.bintray.com/kotlin/kotlinx/'}
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
}

dependencies {
    // MultiPartContent
    implementation project(':landmarks-clientkt')

    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Ktor, the kotlin web framework
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-serialization:$ktor_version"
    // Logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'

    // Kotlin ORM
    implementation 'org.jetbrains.exposed:exposed:0.17.7'
    implementation 'joda-time:joda-time:2.10.5'
    // Fast connection pool for performance
    implementation 'com.zaxxer:HikariCP:3.4.1'
    // SQLite JDBC Driver
    implementation 'org.xerial:sqlite-jdbc:3.27.2.1'
    // Postgresql JDBC Driver
    implementation 'org.postgresql:postgresql:42.2.5'

    // Thumbnail
    implementation 'net.coobird:thumbnailator:0.4.8'
    // GPS extraction
    implementation 'com.drewnoakes:metadata-extractor:2.12.0'

    // JUnit 5 test framework
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.6.0-M1'
    testCompileOnly 'org.junit.platform:junit-platform-runner:1.6.0-M1'

    // Spek, the kotlin test framework, with kotlin version replacement.
    testCompileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompileOnly('org.jetbrains.spek:spek-api:1.2.1') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly('org.jetbrains.spek:spek-junit-platform-engine:1.2.1') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    // Assertion framework
    testImplementation 'org.amshove.kluent:kluent:1.56'
    // Ktor test utility
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"

    // Frontend assets
    implementation 'org.webjars:webjars-locator:0.36'
}

task stage(dependsOn: shadowJar)

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}