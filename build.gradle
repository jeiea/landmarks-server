buildscript {
    ext.kotlin_version = '1.2.61'
    ext.ktor_version = '0.9.4-alpha-2'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'kr.ac.kw.coms.landmarks.server.ApplicationKt'

sourceSets {
    main.resources.srcDirs = [ 'src/main/res' ]
}

jar {
    baseName = 'landmarks-serverkt'
    version = '0.1'
    manifest {
        attributes 'Main-Class': 'kr.ac.kw.coms.landmarks.server.ApplicationKt'
    }
}

repositories {
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlinx'}
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
}

dependencies {
    // MultiPartContent
    implementation project(':landmarks-clientkt')

    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Ktor, the kotlin web framework
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    // Logging
//    implementation 'org.slf4j:slf4j-nop:1.8.0-beta2'
    implementation 'ch.qos.logback:logback-classic:1.2.1'
    // JSON
    implementation 'com.beust:klaxon:3.0.1'
    implementation "io.ktor:ktor-gson:$ktor_version"

    // Kotlin ORM
    implementation 'org.jetbrains.exposed:exposed:0.10.4'
    implementation 'joda-time:joda-time:2.10'
    // Fast connection pool for performance
    implementation 'com.zaxxer:HikariCP:2.6.0'
    // SQLite JDBC Driver
    implementation 'org.xerial:sqlite-jdbc:3.23.1'
    // Postgresql JDBC Driver
    implementation 'org.postgresql:postgresql:42.2.2'

    // Thumbnail
    implementation 'net.coobird:thumbnailator:0.4.8'
    // GPS extraction
    implementation 'com.drewnoakes:metadata-extractor:2.11.0'

    // JUnit 5 test framework
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.2.0'
    testCompileOnly 'org.junit.platform:junit-platform-runner:1.2.0'

    // Spek, the kotlin test framework, with kotlin version replacement.
    testCompileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompileOnly('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    // Assertion framework
    testImplementation 'org.amshove.kluent:kluent:1.38'
    // Ktor test utility
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"

    // Frontend assets
    implementation 'org.webjars:webjars-locator:0.34'
    implementation 'org.webjars:jquery:2.2.4'
    implementation 'org.webjars:jquery-ui:1.11.4'
    implementation 'org.webjars:bootstrap:3.3.6'
}

task stage(dependsOn: shadowJar)

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}
compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}